@ECHO OFF
REM ======================================================================
REM
REM                                Settings
REM
REM ======================================================================
IF NOT EXIST %USERPROFILE%\.Tools\Settings.cmd (EXIT)
CALL %USERPROFILE%\.Tools\Settings.cmd
REM ---------- ytdlp-dl.cmd(SettingsOptions.cmd) ----------
REM SET SHUTDOWN_FLG=0
REM SET YTDLP_PROFILE=default
REM SET DLURL_LIST=%VIDEOS_DIR%\yt-dlp_%yyyy%%mm%%dd%-%hh%%mn%%ss%.txt
REM SET VIDEO_OUTPUT_DIR=%USERPROFILE%\Videos\yt-dlp
REM SET YTDLP_CONFIG_FILE=%VIDEOS_DIR%\yt-dlp.conf
REM SET YTDLP_CONFIG_FILE=%CONFIG_DIR%\%~n0.conf
REM SET YTDLP_DL_LIST_FILE=%DESKTOP_DIR%\dl.txt
REM SET DLURL_HISTORY_DIR=%VIDEOS_DIR%
REM SET MODE=INPUT
REM SET MODE=FILE
REM SET SKIP_FLG=1


REM ======================================================================
REM
REM                                Main
REM
REM ======================================================================
IF NOT DEFINED YTDLP_CONFIG_FILE SET YTDLP_CONFIG_FILE=%VIDEOS_DIR%\yt-dlp.conf
IF NOT DEFINED YTDLP_DL_LIST_FILE SET YTDLP_DL_LIST_FILE=%DESKTOP_DIR%\dl.txt
IF NOT DEFINED DLURL_LIST SET DLURL_LIST=%VIDEOS_DIR%\yt-dlp_%yyyy%%mm%%dd%-%hh%%mn%%ss%.txt
IF NOT DEFINED SHUTDOWN_FLG (SET SHUTDOWN_FLG=0)
IF 1 EQU %SHUTDOWN_FLG% SET /P SHUTDOWN_FLG2="Shutdown? 1:YES 0:NO -> "
IF NOT DEFINED YTDLP_PROFILE (SET YTDLP_PROFILE=default)
IF NOT DEFINED VIDEO_OUTPUT_DIR SET VIDEO_OUTPUT_DIR=%USERPROFILE%\Videos\yt-dlp
IF NOT DEFINED DLURL_HISTORY_DIR SET DLURL_HISTORY_DIR=%VIDEOS_DIR%
SET HISTORY_FILE=history_%yyyy%%mm%.log
IF NOT DEFINED SKIP_FLG (SET /P SKIP_FLG="SET SKIP_FLG(1)=")
IF NOT DEFINED SKIP_FLG (SET SKIP_FLG=1)
IF NOT DEFINED MODE (SET /P MODE_FLG="SET MODE(0:INPUT 1:FILE)=")
IF "%MODE_FLG%"=="1" (
	SET MODE=FILE
) ELSE (
	SET MODE=INPUT
)
SET YTDLP_CONF_OPT=--config-location %YTDLP_CONFIG_FILE%


:COPY_CONF
IF NOT "%1"=="" (
	SET YTDLP_PROFILE_FILE=%~1
	COPY %~1 %YTDLP_CONFIG_FILE%
	NOTEPAD %YTDLP_CONFIG_FILE%
) ELSE (
	SET YTDLP_PROFILE_FILE=yt-dlp.%YTDLP_PROFILE%.dlp
)
IF NOT EXIST %YTDLP_CONFIG_FILE% (COPY %YTDLP_PROFILE_FILE% %YTDLP_CONFIG_FILE%)
ECHO %YTDLP_CONFIG_FILE%
ECHO %YTDLP_CONF_OPT%
TYPE %YTDLP_CONFIG_FILE%

FINDSTR /C:"RESOLUTION" %YTDLP_CONFIG_FILE%
IF ERRORLEVEL 1 (
	REM PASS
) ELSE (
	ECHO SELECT RESOLUTION
	ECHO youtube: 144 240 360 480 720 1080
	ECHO twitch : 160 360 480 720 720 1080
	SET /P RESOLUTION="-> "
)
IF DEFINED RESOLUTION SET YTDLP_RESOLUTION_OPT=-S "res:%RESOLUTION%"

GOTO :DL_START_%MODE%
CD %VIDEOS_DIR%


:DL_START_INPUT
SET /P DOWNLOAD_URL="URL: "
IF %SKIP_FLG%==1 (
	FINDSTR /C:"%DOWNLOAD_URL%" %DLURL_HISTORY_DIR%\*.log
	IF ERRORLEVEL 1 (
		ECHO Start DL
		ECHO %DOWNLOAD_URL%>>%DLURL_HISTORY_DIR%\%HISTORY_FILE%
	) ElSE (
		ECHO Skip:%DOWNLOAD_URL%>>%DLURL_HISTORY_DIR%\%HISTORY_FILE%
		GOTO :DL_START_INPUT
	)
)
yt-dlp.exe %YTDLP_CONF_OPT% %YTDLP_RESOLUTION_OPT% %DOWNLOAD_URL%
EXPLORER %VIDEO_OUTPUT_DIR%
GOTO :DL_START_INPUT


:DL_START_FILE
IF EXIST %YTDLP_DL_LIST_FILE% COPY %YTDLP_DL_LIST_FILE% %DLURL_LIST%
IF NOT EXIST %DLURL_LIST% TYPE nul >%DLURL_LIST%
NOTEPAD %DLURL_LIST%
IF %SKIP_FLG%==1 (
	FOR /F %%i IN (%DLURL_LIST%) DO (
		FINDSTR /C:"%%i" %DLURL_HISTORY_DIR%\*.log
		IF ERRORLEVEL 1 (
			ECHO Start DL: %%i
			ECHO %%i>>%DLURL_LIST%.tmp
		) ELSE (
			ECHO Skip DL: %%i>>"%DLURL_HISTORY_DIR%\%HISTORY_FILE%"
		)
	)
	IF EXIST %DLURL_LIST%.tmp MOVE %DLURL_LIST%.tmp %DLURL_LIST%
)

CD %VIDEOS_DIR%
yt-dlp.exe %YTDLP_CONF_OPT% %YTDLP_RESOLUTION_OPT% -a %DLURL_LIST%

TYPE %DLURL_LIST% >>"%DLURL_HISTORY_DIR%\%HISTORY_FILE%"
ECHO;>>"%DLURL_HISTORY_DIR%\%HISTORY_FILE%"
DEL %DLURL_LIST%
EXPLORER %VIDEO_OUTPUT_DIR%
REM Mp3tag.exe /fp:"%VIDEOS_DIR%\yt-dlp"
GOTO :DL_END


:DL_END
IF 1 EQU %SHUTDOWN_FLG2% (SHUTDOWN /S /T 3)
EXIT
