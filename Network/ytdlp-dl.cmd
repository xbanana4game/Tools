@ECHO OFF
REM ======================================================================
REM
REM                                Settings
REM
REM ======================================================================
IF NOT EXIST %USERPROFILE%\.Tools\Settings.cmd (EXIT)
CALL %USERPROFILE%\.Tools\Settings.cmd
SETLOCAL ENABLEDELAYEDEXPANSION
SET DRIVE_LETTER_FILE=%CD:~0,2%
SET DRIVE_LETTER_CMD=%~d0
REM ---------- ytdlp-dl.cmd(SettingsOptions.cmd) ----------
REM SET SHUTDOWN_FLG=0
REM SET YTDLP_PROFILE=default
REM SET YTDLP_CONF_DIR=%CONFIG_DIR%\yt-dlp
REM SET DLURL_LIST_DIR=%YTDLP_CONF_DIR%
REM SET DLURL_LIST_NAME=_%yyyy%-%mm%-%dd%_T%hh%%mn%%ss%_%~n1
REM SET VIDEO_OUTPUT_DIR=%USERPROFILE%\Videos\yt-dlp
REM SET INPUT_DL_LIST_FILE=%~n1.txt
REM SET YTDLP_DL_LIST_FILE_EXP=%~n1.*.log
REM SET YTDLP_DL_LIST_FILE_EXP=%DESKTOP_DIR%\*.csv.txt
REM SET DLURL_HISTORY_DIR=%CONFIG_DIR%\yt-dlp-history
REM SET DLURL_HISTORY_FILES=%DLURL_HISTORY_DIR%\*.log
REM SET MODE=INPUT
REM SET MODE=FILE
REM SET SKIP_FLG=1
REM SET YT_DLP_LOG_DIR=%CONFIG_DIR%\yt-dlp\yt-dlp-log



REM ======================================================================
REM
REM                                Main
REM
REM ======================================================================
IF NOT "%1"=="" (
	SET YTDLP_PROFILE_FILE=%~1
	SET YTDLP_PROFILE=%~n1
) ELSE (
	FOR %%i IN (%CONFIG_DIR%\yt-dlp\*.dlp) DO (
		ECHO %%~ni
	)
	SET /P YTDLP_PROFILE="->"
	SET YTDLP_PROFILE_FILE=%CONFIG_DIR%\yt-dlp\!YTDLP_PROFILE!.dlp
)
IF NOT DEFINED YTDLP_PROFILE (SET YTDLP_PROFILE=default)
IF NOT DEFINED END_FILE_EXT SET END_FILE_EXT=list
IF NOT DEFINED VIDEO_OUTPUT_DIR SET VIDEO_OUTPUT_DIR=%USERPROFILE%\Videos\yt-dlp
IF NOT EXIST %VIDEO_OUTPUT_DIR% MD %VIDEO_OUTPUT_DIR%
IF NOT DEFINED YTDLP_DL_LIST_FILE_EXP SET YTDLP_DL_LIST_FILE_EXP=%YTDLP_PROFILE%.*.%END_FILE_EXT%
IF NOT DEFINED INPUT_DL_LIST_FILE SET INPUT_DL_LIST_FILE=%YTDLP_PROFILE%.txt
IF NOT DEFINED YTDLP_CONF_DIR SET YTDLP_CONF_DIR=%CONFIG_DIR%\yt-dlp
IF NOT DEFINED DLURL_LIST_DIR SET DLURL_LIST_DIR=%YTDLP_CONF_DIR%
IF NOT DEFINED DLURL_LIST_NAME SET DLURL_LIST_NAME=_%yyyy%-%mm%-%dd%_T%hh%%mn%%ss%_%YTDLP_PROFILE%
IF NOT DEFINED DLURL_LIST SET DLURL_LIST=%DLURL_LIST_DIR%\%DLURL_LIST_NAME%
IF NOT DEFINED YTDLP_CONFIG_FILE SET YTDLP_CONFIG_FILE=%DLURL_LIST%.dlp
IF NOT DEFINED DLURL_HISTORY_DIR SET DLURL_HISTORY_DIR=%CONFIG_DIR%\yt-dlp-history
IF NOT DEFINED DLURL_HISTORY_FILES SET DLURL_HISTORY_FILES=%DLURL_HISTORY_DIR%\*.log
IF NOT DEFINED YT_DLP_LOG_DIR SET YT_DLP_LOG_DIR=%CONFIG_DIR%\yt-dlp\yt-dlp-log
IF NOT DEFINED BATCH_MODE SET BATCH_MODE=0

IF NOT EXIST %YTDLP_CONF_DIR% MD %YTDLP_CONF_DIR%
IF NOT EXIST %DLURL_HISTORY_DIR% MD %DLURL_HISTORY_DIR%
SET YT_DLP_ERR_FILE_NAME=%DLURL_LIST_NAME%_%yyyy%%mm%%dd%%hh%%mn%%ss%.err
SET YTDLP_ERR_FILE=%VIDEOS_DIR%\%YT_DLP_ERR_FILE_NAME%

IF "%BATCH_MODE%"=="ON" GOTO :DL_START_BATCH


:SET_YTDLP_CONFIG_FILE
COPY %YTDLP_PROFILE_FILE% %YTDLP_CONFIG_FILE%
IF NOT EXIST %YTDLP_CONFIG_FILE% (EXIT)
REM TYPE %YTDLP_CONFIG_FILE%
ECHO YTDLP_CONFIG_FILE: %YTDLP_CONFIG_FILE%
SET YTDLP_CONF_OPT=--config-location %YTDLP_CONFIG_FILE%

CALL :SET_SKIP_MODE
ECHO SKIP_FLG: %SKIP_FLG%
ECHO SKIP_QUALITY: %SKIP_QUALITY%
ECHO DLURL_HISTORY_FILES: %DLURL_HISTORY_FILES%
IF DEFINED SKIP_QUALITY DIR /B %DLURL_HISTORY_FILES%

CALL :SET_RESOLUTION
ECHO YTDLP_RESOLUTION_OPT: %YTDLP_RESOLUTION_OPT%

:SET_MODE
FINDSTR /C:"INPUT_MODE" %YTDLP_CONFIG_FILE%
IF ERRORLEVEL 1 (
	REM PASS
) ELSE (
	SET MODE=INPUT
	GOTO :DL_START_INPUT
)

:SET_INPUT_MODE
FOR %%i in (%YTDLP_DL_LIST_FILE_EXP%) DO (
	SET INPUT_DL_LIST_FILE=%%i
)
CHOICE /C 12345 /M "1:INPUT 2:FILE 3:BATCH 4:PLAYLIST 5:FORMAT"
IF %ERRORLEVEL% EQU 1 (
	SET MODE=INPUT
) ELSE IF %ERRORLEVEL% EQU 2 (
	SET MODE=FILE
	ECHO DEFAULT: %INPUT_DL_LIST_FILE%
	FOR %%i in (%DESKTOP_DIR%\*.txt) DO ECHO %%~fi
	IF NOT EXIST %INPUT_DL_LIST_FILE% SET /P INPUT_DL_LIST_FILE="input txt: "
) ELSE IF %ERRORLEVEL% EQU 3 (
	SET MODE=FILE
	SET BATCH_ONLY=1
	MOVE %YTDLP_CONFIG_FILE% %DLURL_LIST%_batch.dlp
	SET YTDLP_CONFIG_FILE=%DLURL_LIST%_batch.dlp
	SET DLURL_LIST_NAME=%DLURL_LIST_NAME%_batch
	SET DLURL_LIST=%DLURL_LIST%_batch
	ECHO DEFAULT: %INPUT_DL_LIST_FILE%
	FOR %%i in (%DESKTOP_DIR%\*.txt) DO ECHO %%~fi
	IF NOT EXIST %INPUT_DL_LIST_FILE% SET /P INPUT_DL_LIST_FILE="input txt: "
) ELSE IF %ERRORLEVEL% EQU 4 (
	SET MODE=PLAYLIST
) ELSE IF %ERRORLEVEL% EQU 5 (
	SET MODE=FORMAT
	GOTO :CHECK_FORMAT
)
SET YTDLP_CONF_OPT=--config-location %YTDLP_CONFIG_FILE%
ECHO DL_START_%MODE%

SET _YTDLP_DL_LIST_FILE=%INPUT_DL_LIST_FILE:"=%
SET FILE_EXT=%_YTDLP_DL_LIST_FILE:~-7,-4%
SET FILE_EXT_LAST=%_YTDLP_DL_LIST_FILE:~-3%
IF "%FILE_EXT%"=="csv" (
	SET DLURL_LIST=%DLURL_LIST%.csv.txt
) ELSE (
	SET DLURL_LIST=%DLURL_LIST%.txt
)
SET YTDLP_DLURL_LIST_NAME=%DLURL_LIST_NAME%.txt
SET YTDLP_DLURL_LIST=%DLURL_LIST_DIR%\%YTDLP_DLURL_LIST_NAME%.%END_FILE_EXT%

GOTO :DL_START_%MODE%
EXIT /B


:DL_START_INPUT
SET DOWNLOAD_URL=
SET /P DOWNLOAD_URL="URL: "
CD /D %VIDEOS_DIR%
yt-dlp.exe %YTDLP_CONF_OPT% %YTDLP_RESOLUTION_OPT% "%DOWNLOAD_URL%"
IF %ERRORLEVEL% EQU 0 (
	TIMEOUT /T 3
) ELSE (
	PAUSE
)
DEL %YTDLP_CONFIG_FILE%
GOTO :DL_END
EXIT /B


:DL_START_PLAYLIST
SET /P DOWNLOAD_URL="URL: "
SET /P PLAYLIST_START="PLAYLIST START NUMBER: "
SET YTDLP_OPT=--yes-playlist
IF DEFINED PLAYLIST_START SET YTDLP_OPT=%YTDLP_OPT% --playlist-start %PLAYLIST_START%
CD /D %VIDEOS_DIR%
yt-dlp.exe %YTDLP_CONF_OPT% %YTDLP_RESOLUTION_OPT% %YTDLP_OPT% "%DOWNLOAD_URL%" 2>%YTDLP_ERR_FILE%
DEL %YTDLP_CONFIG_FILE%
GOTO :DL_END
EXIT /B


:DL_START_FILE
IF EXIST %INPUT_DL_LIST_FILE% COPY %INPUT_DL_LIST_FILE% %DLURL_LIST%
IF NOT EXIST %DLURL_LIST% TYPE nul >%DLURL_LIST%
SET YTDLP_LOCK_FILE=%DESKTOP_DIR%\%DLURL_LIST_NAME%.lock
NOTEPAD %DLURL_LIST%
IF "%FILE_EXT_LAST%"=="%END_FILE_EXT%" (
	DEL %INPUT_DL_LIST_FILE%
	DEL %YTDLP_PROFILE_FILE%
)
IF %SKIP_FLG%==1 (
	CALL :FIND_HISTORY %DLURL_LIST%
)
CALL :MAKE_BATCH_FILE
IF DEFINED BATCH_ONLY EXIT /B
CALL :IS_FILE_EMPTY %YTDLP_DLURL_LIST%
IF %ERRORLEVEL% EQU 0 (
	GOTO :DL_END
)
ECHO;>%YTDLP_LOCK_FILE%
CD /D %VIDEOS_DIR%
yt-dlp.exe %YTDLP_CONF_OPT% %YTDLP_RESOLUTION_OPT% -a %YTDLP_DLURL_LIST% 2>%YTDLP_ERR_FILE%
IF %ERRORLEVEL% EQU 0 (
	REM NO ERROR
	DEL %YTDLP_ERR_FILE%
)
GOTO :DL_END
EXIT /B


:DL_START_BATCH
SET YTDLP_CONFIG_FILE=%~1
SET DLURL_LIST_DIR=%~p1
SET DLURL_LIST_NAME=%~n1
SET YTDLP_DLURL_LIST=%~2
IF NOT EXIST %YTDLP_CONFIG_FILE% EXIT /B 1
IF NOT EXIST %YTDLP_DLURL_LIST% EXIT /B 1
SET YTDLP_CONF_OPT=--config-location %YTDLP_CONFIG_FILE%
SET YTDLP_ERR_FILE=%VIDEOS_DIR%\%DLURL_LIST_NAME%_%yyyy%%mm%%dd%%hh%%mn%%ss%.err
SET YTDLP_LOCK_FILE=%DESKTOP_DIR%\%DLURL_LIST_NAME%.lock
ECHO BATCH_MODE: %DLURL_LIST_NAME%
IF EXIST %YTDLP_LOCK_FILE% EXIT /B 1
IF EXIST %DESKTOP_DIR%\%DLURL_LIST_NAME%*.err EXIT /B 1
ECHO;>%YTDLP_LOCK_FILE%
IF %SKIP_FLG%==1 (
	CALL :FIND_HISTORY %YTDLP_DLURL_LIST%
)
CALL :IS_FILE_EMPTY %YTDLP_DLURL_LIST%
IF %ERRORLEVEL% EQU 0 (
	CALL :DL_END
	EXIT /B 1
)
CD /D %VIDEOS_DIR%
yt-dlp.exe %YTDLP_CONF_OPT% %YTDLP_BATCH_OPT% %YTDLP_OPT% -a %YTDLP_DLURL_LIST% 2>%YTDLP_ERR_FILE%
IF %ERRORLEVEL% EQU 0 (
	REM NO ERROR
	DEL %YTDLP_ERR_FILE%
)
CALL :DL_END
EXIT /B 0


:DL_END
IF NOT EXIST %YT_DLP_LOG_DIR% MD %YT_DLP_LOG_DIR%
IF DEFINED YTDLP_LOCK_FILE DEL %YTDLP_LOCK_FILE%
IF EXIST %YTDLP_ERR_FILE% (
	FINDSTR /C:"ERROR" %YTDLP_ERR_FILE%
	IF ERRORLEVEL 1 (
		REM NO ERROR
		DEL %YTDLP_ERR_FILE%
	) ELSE (
		REM ERROR
		FINDSTR /C:"ERROR" %YTDLP_ERR_FILE%>%DESKTOP_DIR%\%DLURL_LIST_NAME%.err
		IF NOT EXIST %YT_DLP_LOG_DIR% MD %YT_DLP_LOG_DIR%
		MOVE %YTDLP_ERR_FILE% %YT_DLP_LOG_DIR%\
		EXIT /B 1
	)
)
ECHO MOVE LOG DIR: %DLURL_LIST_DIR%\%DLURL_LIST_NAME%*
FOR %%F IN (%DLURL_LIST_DIR%\%DLURL_LIST_NAME%*) DO (
	IF EXIST %%F MOVE %%F %YT_DLP_LOG_DIR%\
)
EXIT /B 0



REM ======================================================================
REM
REM                                Function
REM
REM ======================================================================
:SET_SKIP_MODE
FINDSTR /C:"#SKIP_MODE_OFF" %YTDLP_CONFIG_FILE%
IF ERRORLEVEL 1 (
	REM PASS
) ELSE (
	SET SKIP_FLG=0
	EXIT /B
)

FINDSTR /C:"#SKIP_MODE_ON_PREFIX" %YTDLP_CONFIG_FILE%
IF ERRORLEVEL 1 (
	REM PASS
) ELSE (
	SET SKIP_FLG=1
	FOR /f "usebackq" %%A IN (`ytdlp_profile.py`) do SET YTDLP_DOMAIN=%%A
	SET DLURL_HISTORY_FILES=%DLURL_HISTORY_DIR%\_!YTDLP_DOMAIN!-add*.log
	SET SKIP_QUALITY=%YTDLP_PROFILE%
	ECHO !DLURL_HISTORY_FILES!
	EXIT /B
)
FINDSTR /C:"#SKIP_MODE_ON" %YTDLP_CONFIG_FILE%
IF ERRORLEVEL 1 (
	REM PASS
) ELSE (
	SET SKIP_FLG=1
	EXIT /B
)
CHOICE /C 01 /T 1 /D 1 /M "SKIP MODE "
IF %ERRORLEVEL% EQU 1 (
	ECHO SKIP MODE OFF
	ECHO #SKIP_MODE_OFF>>%YTDLP_CONFIG_FILE%
	SET SKIP_FLG=0
) ELSE IF %ERRORLEVEL% EQU 2 (
	ECHO SKIP MODE ON
	SET SKIP_FLG=1
)
IF NOT DEFINED SKIP_FLG (SET SKIP_FLG=1)
EXIT /B


:SET_RESOLUTION
FINDSTR /C:"RESOLUTION" %YTDLP_CONFIG_FILE%
IF ERRORLEVEL 1 (
	REM PASS
) ELSE (
	ECHO SELECT RESOLUTION
	ECHO youtube: 144 240 360 480 720 1080
	ECHO twitch : 160 360 480 720 720 1080
	SET /P RESOLUTION="-> "
)
IF DEFINED RESOLUTION (
	SET YTDLP_RESOLUTION_OPT=-S "res:%RESOLUTION%"
	ECHO #-S "res:%RESOLUTION%">>%YTDLP_CONFIG_FILE%
)
EXIT /B


:FIND_HISTORY
SET INPUT_DLURL_LIST_PATH=%1
IF NOT DEFINED INPUT_DLURL_LIST_PATH EXIT 1
IF NOT DEFINED YTDLP_DLURL_LIST EXIT 1
SET STRIP_URL_LIST_FILE=%VIDEOS_DIR%\strip_url.txt
ECHO strip_url.py %INPUT_DLURL_LIST_PATH% %STRIP_URL_LIST_FILE%
strip_url.py %INPUT_DLURL_LIST_PATH% %STRIP_URL_LIST_FILE%
IF ERRORLEVEL 1 (
	DEL %STRIP_URL_LIST_FILE%
	PAUSE
	EXIT 1
)
IF NOT DEFINED DLURL_RESERVED_FILE (
	SET DLURL_RESERVED_FILE=%YTDLP_CONF_DIR%\*.%END_FILE_EXT%
	REM When batch mode, exclude self url list file. delete or not find list file
	DEL %YTDLP_DLURL_LIST%
)
ECHO FINDSTR /C:"URL" %DLURL_HISTORY_FILES% %DLURL_RESERVED_FILE%
FOR /F "tokens=1,2 delims=," %%i IN (%STRIP_URL_LIST_FILE%) DO (
	FINDSTR /C:"%%j" %DLURL_HISTORY_FILES% %DLURL_RESERVED_FILE% >nul 2>&1
	IF ERRORLEVEL 1 (
		ECHO DL:%%i
		ECHO %%i>>%YTDLP_DLURL_LIST%
	) ELSE (
		ECHO Skip:%%i
		ECHO #Skip:%%i>>%YTDLP_CONFIG_FILE%
	)
)
IF NOT EXIST %YTDLP_DLURL_LIST% TYPE nul>%YTDLP_DLURL_LIST%
IF EXIST %STRIP_URL_LIST_FILE% DEL %STRIP_URL_LIST_FILE%
EXIT /B 0


:MAKE_BATCH_FILE
SET BATCH_FILE=%YTDLP_CONF_DIR%\yt-dlp-batch_%yyyy%%mm%%dd%.cmd
IF NOT EXIST %BATCH_FILE% (
	ECHO SET BATCH_MODE=ON>>%BATCH_FILE%
	REM ECHO SET MODE=BATCH>>%BATCH_FILE%
	ECHO CALL %USERPROFILE%\.Tools\Settings.cmd>>%BATCH_FILE%
	ECHO SET BATCH_LIST_DIR=%YTDLP_CONF_DIR%>>%BATCH_FILE%
	ECHO SET YTDLP_BATCH_OPT=>>%BATCH_FILE%
	ECHO;>>%BATCH_FILE%
)
ECHO :%DLURL_LIST_NAME%_START>>%BATCH_FILE%
ECHO (SET SKIP_FLG=%SKIP_FLG%)>>%BATCH_FILE%
ECHO SET DLURL_HISTORY_FILES=%DLURL_HISTORY_FILES%>>%BATCH_FILE%
IF DEFINED SKIP_QUALITY (
	ECHO SET SKIP_QUALITY=%SKIP_QUALITY%>>%BATCH_FILE%
)
IF DEFINED RESOLUTION (
	ECHO SET YTDLP_OPT=-S "res:%RESOLUTION%">>%BATCH_FILE%
) ELSE (
	ECHO SET YTDLP_OPT=>>%BATCH_FILE%
)

ECHO CALL ytdlp-dl.cmd %%BATCH_LIST_DIR%%\%DLURL_LIST_NAME%.dlp %%BATCH_LIST_DIR%%\%YTDLP_DLURL_LIST_NAME%.%END_FILE_EXT%>>%BATCH_FILE%
ECHO REM yt-dlp.exe --config-location %%BATCH_LIST_DIR%%\%DLURL_LIST_NAME%.dlp %%YTDLP_BATCH_OPT%% %%YTDLP_OPT%% -a %%BATCH_LIST_DIR%%\%YTDLP_DLURL_LIST_NAME%.%END_FILE_EXT%>>%BATCH_FILE%
ECHO :%DLURL_LIST_NAME%_END>>%BATCH_FILE%
ECHO;>>%BATCH_FILE%
EXIT /B


:CHECK_FORMAT
REM SET YTDLP_OPT=-v -F --list-subs
SET YTDLP_OPT=-v -F
SET DOWNLOAD_URL=
SET /P DOWNLOAD_URL="URL: "
IF NOT DEFINED DOWNLOAD_URL GOTO :CHECK_FORMAT_END
REM SET /P FORMAT_OPTION="FORMAT_OPTION: "
yt-dlp.exe %YTDLP_CONF_OPT% %YTDLP_OPT% %FORMAT_OPTION% %DOWNLOAD_URL%
GOTO :CHECK_FORMAT
:CHECK_FORMAT_END
DEL %YTDLP_CONFIG_FILE%
EXIT /B


:IS_FILE_EMPTY
FOR %%F IN (%1) DO (
	IF %%~zF EQU 0 (
		REM File size is zero
		EXIT /B 0
	)
)
EXIT /B 1


