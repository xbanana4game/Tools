@ECHO OFF
REM ======================================================================
REM
REM                                Settings
REM
REM ======================================================================
IF NOT EXIST %USERPROFILE%\.Tools\Settings.cmd (EXIT)
CALL %USERPROFILE%\.Tools\Settings.cmd
SET DRIVE_LETTER_FILE=%CD:~0,2%
SET DRIVE_LETTER_CMD=%~d0
SET DRIVE_LETTER=%DRIVE_LETTER_CMD%
SETLOCAL ENABLEDELAYEDEXPANSION

REM ---------- Archive Directory(SettingsOptions.cmd) ----------
REM SET SDEL_FLG=0
REM SET ARCHIVE_OUTPUT_DIR=%DRIVE_LETTER%\rar
REM SET ARCHIVE_OUTPUT_DIR=%DESKTOP_DIR%\rar
REM SET Z_PASSWORD=NONE
REM SET Z_PASSWORD=%DOWNLOADS_PASSWORD%
REM x=[0 | 1 | 3 | 5 ] 
REM SET OPT_COMPRESSION_LEVEL=0
REM SET WINRAR_LOG=winrar-%yyyy%%mm%%dd%%hh%%mn%%ss%.log
REM SET WORK_DIR=C:\tmp
REM SET RECOVERY_VOLUME_NUMBER=1
REM --------------------------------------
REM BD-R 23.3 GB (25,023,217,664 バイト)
REM SET VOLUME_SIZE=23864m
REM SET VOLUME_SIZE=11932m
REM SET VOLUME_SIZE=7954m
REM BD-Windows UDF 22.5GB 24,218,238,976バイト
REM SET VOLUME_SIZE=23096m
REM Archive
REM SET VOLUME_SIZE=0
REM SET VOLUME_SIZE=1g


REM ======================================================================
REM
REM                                Main
REM
REM ======================================================================
IF NOT DEFINED OPT_COMPRESSION_LEVEL (SET /P OPT_COMPRESSION_LEVEL="SET OPT_COMPRESSION_LEVEL(0)=")
IF NOT DEFINED OPT_COMPRESSION_LEVEL (SET OPT_COMPRESSION_LEVEL=0)
IF NOT DEFINED Z_PASSWORD (SET /P Z_PASSWORD="SET Z_PASSWORD(NONE)=")
IF NOT DEFINED Z_PASSWORD (SET Z_PASSWORD=NONE)
REM IF NOT DEFINED ARCHIVE_OUTPUT_DIR (SET /P ARCHIVE_OUTPUT_DIR="SET ARCHIVE_OUTPUT_DIR(%DRIVE_LETTER%\rar)=")
IF NOT DEFINED ARCHIVE_OUTPUT_DIR (SET ARCHIVE_OUTPUT_DIR=%DRIVE_LETTER%\rar)
IF NOT DEFINED VOLUME_SIZE (
	ECHO BD-R   : 23864m
	ECHO Archive: 1g
	SET /P VOLUME_SIZE="VOLUME_SIZE(0):"
)
IF NOT DEFINED VOLUME_SIZE (SET VOLUME_SIZE=0)
IF NOT %VOLUME_SIZE%==0 SET OPT_VOLUME=-v%VOLUME_SIZE%
IF NOT DEFINED RECOVERY_VOLUME_NUMBER SET /P RECOVERY_VOLUME_NUMBER="RECOVERY_VOLUME_NUMBER(1):"
IF NOT DEFINED RECOVERY_VOLUME_NUMBER SET RECOVERY_VOLUME_NUMBER=1
IF NOT DEFINED SDEL_FLG (
	CHOICE /C 01 /M "SDEL_FLG:"
	IF !ERRORLEVEL! EQU 1 (
		SET SDEL_FLG=0
	) ElSE IF !ERRORLEVEL! EQU 2 (
		SET SDEL_FLG=1
	)
)
IF "%SDEL_FLG%"=="1" (SET SDEL_OPT=-dr)
IF NOT %RECOVERY_VOLUME_NUMBER%==0 SET RECOVERY_VOLUME_OPTIONS=-rv%RECOVERY_VOLUME_NUMBER%
REM IF NOT DEFINED WINRAR_LOG SET WINRAR_LOG=winrar-%yyyy%%mm%%dd%%hh%%mn%%ss%.log
IF DEFINED WINRAR_LOG SET WINRAR_LOG_OPTIONS=-ilog%WINRAR_LOG%
IF DEFINED WORK_DIR SET WORK_DIR_OPTIONS=-w%WORK_DIR%
IF NOT "%Z_PASSWORD%"=="NONE" SET ARCHIVE_OPT_PW=-hp%Z_PASSWORD%

SET WINRAR_OPTIONS=%WORK_DIR_OPTIONS% %SDEL_OPT% %ARCHIVE_OPT_PW% -rr3p %RECOVERY_VOLUME_OPTIONS% %OPT_VOLUME% %WINRAR_LOG_OPTIONS% -m%OPT_COMPRESSION_LEVEL%
FOR %%i in (%*) DO (
	ECHO ====================== Winrar ======================
	ECHO OPTIONS: %SDEL_OPT% %ARCHIVE_OPT_PW% -rr3p %RECOVERY_VOLUME_OPTIONS% %OPT_VOLUME% %WINRAR_LOG_OPTIONS% -m%OPT_COMPRESSION_LEVEL%
	ECHO INPUT  : "%%~i"
	ECHO OUTPUT : "%ARCHIVE_OUTPUT_DIR%\%%~nxi.rar"
	ECHO ====================================================
)
PAUSE
IF NOT EXIST %ARCHIVE_OUTPUT_DIR% MD %ARCHIVE_OUTPUT_DIR%
FOR %%i in (%*) DO (
	winrar a %WORK_DIR_OPTIONS% %SDEL_OPT% %ARCHIVE_OPT_PW% -rr3p %RECOVERY_VOLUME_OPTIONS% %OPT_VOLUME% %WINRAR_LOG_OPTIONS% -m%OPT_COMPRESSION_LEVEL% "%ARCHIVE_OUTPUT_DIR%\%%~nxi.rar" "%%~i"
)
PAUSE
EXIT

