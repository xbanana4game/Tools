@ECHO OFF
REM ======================================================================
REM
REM                                Settings
REM
REM ======================================================================
IF NOT EXIST %USERPROFILE%\.Tools\Settings.cmd (EXIT)
CALL %USERPROFILE%\.Tools\Settings.cmd
REM if  called this batch from Archive.cmd ARCHIVE_FLG is 1. 
IF NOT DEFINED ARCHIVE_FLG (SET ARCHIVE_FLG=0)

REM ---------- MoveFiles.cmd(MoveFiles-user.cmd) ----------
REM SET MOVE_VIDEOS=1
REM SET MOVE_VIDEOS_TARGET_DIR=Captures Videos
REM SET DOWNLOADED_VIDEOS_DIR=%DOWNLOADS_DIR%
REM SET VIDEOS_STORE_DIR=%STORE_DIR%\VD_%yyyy%%mm%%dd%
REM SET COPY_VIDEOS=1
REM SET XCOPY_DIRECTORY_VIDEOS=\\%NASDOMAIN%\Multimedia\Videos

REM SET MOVE_VHDX=1
REM SET CREATED_VHDX_DIR=%DOWNLOADS_DIR%
REM SET VHDX_STORE_DIR=%STORE_DIR%\VHDX_%yyyy%%mm%%dd%
REM SET COPY_VHDX=1
REM SET COPY_DIRECTORY_VHDX=\\%NASDOMAIN%\Multimedia\VHDX_%yyyy%%mm%%dd%

REM SET XCOPY_XXX=1
REM SET XCOPY_DIRECTORY_XXX=\\%NASDOMAIN%\Multimedia\Watch\xxx

REM SET XCOPY_DIRECTORY=\\%NASDOMAIN%\Multimedia
REM SET COPY_NAS_TARGET_DIR=Books Music Anime Movies

REM SET SAVE_STEAM_SS=1
REM SET SAVE_ALL_STEAM_SS=0

REM SET SAVE_OSU_SS=1



REM ======================================================================
REM
REM                                Main
REM
REM ======================================================================
IF EXIST %USERPROFILE%\.Tools\MoveFiles-user.cmd (CALL %USERPROFILE%\.Tools\MoveFiles-user.cmd)

IF NOT DEFINED VIDEOS_STORE_DIR SET VIDEOS_STORE_DIR=%STORE_DIR%\VD_%yyyy%%mm%%dd%
IF NOT DEFINED DOWNLOADED_VIDEOS_DIR SET DOWNLOADED_VIDEOS_DIR=%DOWNLOADS_DIR%
IF NOT DEFINED CREATED_VHDX_DIR SET CREATED_VHDX_DIR=%DOWNLOADS_DIR%
IF NOT DEFINED COPY_NAS_TARGET_DIR SET COPY_NAS_TARGET_DIR=Books Music Anime Movies
IF NOT DEFINED MOVE_VIDEOS_TARGET_DIR SET MOVE_VIDEOS_TARGET_DIR=Captures Videos

IF NOT DEFINED ROBOCOPY_LOG SET ROBOCOPY_LOG=%DESKTOP_DIR%\robocopy-%yyyy%%mm%%dd%%hh%%mn%%ss%.log
IF NOT DEFINED ROBOCOPY_LOG_OPTIONS SET ROBOCOPY_LOG_OPTIONS=/log+:"%ROBOCOPY_LOG%" /v /fp /tee
IF NOT DEFINED ROBOCOPY_COPY_OPTIONS SET ROBOCOPY_COPY_OPTIONS=/e /r:3 /w:10


IF DEFINED MOVE_VHDX CALL :MOVE_VHDX
IF DEFINED XCOPY_XXX CALL :F_XCOPY_XXX
IF DEFINED MOVE_VIDEOS (
	FOR %%i IN (%MOVE_VIDEOS_TARGET_DIR%) DO (
		CALL :MOVE_VIDEOS %%i
	)
	IF 1 EQU %COPY_VIDEOS% CALL :COPY_VIDEOS
)
IF DEFINED COPY_NAS_TARGET_DIR (
	FOR %%i IN (%COPY_NAS_TARGET_DIR%) DO (
		CALL :F_XCOPY_DIR %%i
	)
)
IF DEFINED SAVE_STEAM_SS CALL :SAVE_STEAM_SS
IF DEFINED SAVE_OSU_SS CALL :SAVE_OSU_SS
IF 1 EQU %ARCHIVE_FLG% (
	EXIT /B
)
PAUSE
EXIT /B


REM ======================================================================
REM
REM                                Function
REM
REM ======================================================================
:MOVE_TARGET_FILES
	SET TARGET_DIR=%1
	SET TARGET_FILE=%2
	IF NOT EXIST %TARGET_FILE% EXIT /B
	MD %TARGET_DIR%
	MOVE %TARGET_FILE% %TARGET_DIR%\
	EXIT /B
	
:MOVE_VHDX
	ECHO MOVE VHDX
	MD %VHDX_STORE_DIR%
	IF EXIST %CREATED_VHDX_DIR%\*.vhdx MOVE %CREATED_VHDX_DIR%\*.vhdx %VHDX_STORE_DIR%
	RMDIR %VHDX_STORE_DIR%
	IF EXIST %VHDX_STORE_DIR% EXPLORER %VHDX_STORE_DIR%
	IF "%COPY_VHDX%"=="1" CALL :COPY_VHDX
	EXIT /B
	
:COPY_VHDX
	IF NOT DEFINED COPY_DIRECTORY_VHDX EXIT /B
	IF NOT EXIST %VHDX_STORE_DIR% EXIT /B
	CHOICE /C YN /T 3 /D Y /M "COPY VHDX %VHDX_STORE_DIR%? -> %COPY_DIRECTORY_VHDX%"
	IF %ERRORLEVEL% EQU 2 (EXIT /B)
	ROBOCOPY %VHDX_STORE_DIR% %COPY_DIRECTORY_VHDX% %ROBOCOPY_COPY_OPTIONS% %ROBOCOPY_LOG_OPTIONS%
	IF %ERRORLEVEL% EQU 8 (
		NOTEPAD %ROBOCOPY_LOG%
	) ELSE (
		EXPLORER %COPY_DIRECTORY_VHDX%
		DEL %ROBOCOPY_LOG%
	)
	EXIT /B
	
:MOVE_VIDEOS
	SET TARGET_DIR=%1
	SET PLAYLIST_FILENAME=%TARGET_DIR%_%yyyy%-%mm%-%dd%
	ECHO MOVE %TARGET_DIR% %VIDEOS_STORE_DIR%
	MD %VIDEOS_STORE_DIR%
	IF EXIST %DOWNLOADED_VIDEOS_DIR%\%TARGET_DIR% MOVE %DOWNLOADED_VIDEOS_DIR%\%TARGET_DIR% %VIDEOS_STORE_DIR%\
	IF NOT EXIST %VIDEOS_STORE_DIR%\%TARGET_DIR% (EXIT /B)
	RMDIR %VIDEOS_STORE_DIR%
	ECHO Playlist file name is %VIDEOS_STORE_DIR%\%TARGET_DIR%\%PLAYLIST_FILENAME%.m3u8
	IF EXIST %VIDEOS_STORE_DIR%\%TARGET_DIR% Mp3tag.exe /fp:"%VIDEOS_STORE_DIR%\%TARGET_DIR%"
	IF EXIST %VIDEOS_STORE_DIR%\%TARGET_DIR%\%PLAYLIST_FILENAME%.m3u8 (
		ECHO Execute macro m3u2wpl.ppa. alt+q
		%SAKURA% %VIDEOS_STORE_DIR%\%TARGET_DIR%\%PLAYLIST_FILENAME%.m3u8
		MD %VIDEOS_STORE_DIR%\%TARGET_DIR%\__playlist
	)
	CALL :UPDATE_VIDEO_DB %VIDEOS_STORE_DIR%\%TARGET_DIR%
	EXIT /B

:COPY_VIDEOS
	IF NOT DEFINED XCOPY_DIRECTORY_VIDEOS EXIT /B
	RMDIR %VIDEOS_STORE_DIR%
	IF NOT EXIST %VIDEOS_STORE_DIR% EXIT /B
	CHOICE /C YN /T 5 /D Y /M "COPY VIDEOS %VIDEOS_STORE_DIR%? -> %XCOPY_DIRECTORY_VIDEOS%"
	IF %ERRORLEVEL% EQU 2 (EXIT /B)
	ROBOCOPY %VIDEOS_STORE_DIR% %XCOPY_DIRECTORY_VIDEOS% %ROBOCOPY_COPY_OPTIONS% %ROBOCOPY_LOG_OPTIONS%
	IF %ERRORLEVEL% EQU 8 (
		NOTEPAD %ROBOCOPY_LOG%
	) ELSE (
		REM EXPLORER %XCOPY_DIRECTORY_VIDEOS%
		DEL %ROBOCOPY_LOG%
	)
	IF DEFINED NASDOMAIN START http://%NASDOMAIN%:9000/webconfig#advanced
	EXIT /B
	
:SAVE_STEAM_SS
	ECHO ARCHIVE STEAM SS
	IF DEFINED SAVE_ALL_STEAM_SS SET SAVE_ALL_STEAM_SS_FLG=%SAVE_ALL_STEAM_SS%
	CALL Steam-Screenshots.cmd
	EXIT /B

:SAVE_OSU_SS
	ECHO SAVE OSU SS
	CALL %TOOLS_DIR%\Osu!\Archive-Osu!-Screenshot.cmd
	EXIT /B

:F_XCOPY_XXX
	IF NOT DEFINED XCOPY_DIRECTORY_XXX EXIT /B
	IF NOT EXIST %DOWNLOADED_VIDEOS_DIR%\xxx EXIT /B
	Mp3tag.exe /fp:"%DOWNLOADED_VIDEOS_DIR%\xxx"
	CALL :UPDATE_VIDEO_DB %DOWNLOADED_VIDEOS_DIR%\xxx
	SET TARGET_XXX_FILE=*5star* *4star*
	REM SET EXTRA_OPTIONS=/XD *-low
	ROBOCOPY %DOWNLOADED_VIDEOS_DIR%\xxx %XCOPY_DIRECTORY_XXX% %TARGET_XXX_FILE% %EXTRA_OPTIONS% %ROBOCOPY_COPY_OPTIONS% %ROBOCOPY_LOG_OPTIONS%
	IF %ERRORLEVEL% EQU 8 (
		NOTEPAD %ROBOCOPY_LOG%
	) ELSE (
		EXPLORER %XCOPY_DIRECTORY_XXX%
		DEL %ROBOCOPY_LOG%
	)
	EXIT /B

:F_XCOPY_DIR
	SET DIR_NAME=%1
	IF NOT DEFINED XCOPY_DIRECTORY EXIT /B
	IF NOT EXIST %DOWNLOADS_DIR%\%DIR_NAME% (
		ECHO %DOWNLOADS_DIR%\%DIR_NAME% not exist.
		EXIT /B
	)
	ECHO Start Copy %DOWNLOADS_DIR%\%DIR_NAME%
	CALL :IS_EXIST_MP4 %DOWNLOADS_DIR%\%DIR_NAME%
	IF %ERRORLEVEL% EQU 0 (
		Mp3tag.exe /fp:"%DOWNLOADS_DIR%\%DIR_NAME%"
		CALL :UPDATE_VIDEO_DB %DOWNLOADS_DIR%\%DIR_NAME%
	)
	ROBOCOPY %DOWNLOADS_DIR%\%DIR_NAME% %XCOPY_DIRECTORY%\%DIR_NAME%\ %ROBOCOPY_COPY_OPTIONS% %ROBOCOPY_LOG_OPTIONS%
	IF %ERRORLEVEL% EQU 8 (
		NOTEPAD %ROBOCOPY_LOG%
	) ELSE (
		EXPLORER %XCOPY_DIRECTORY%\%DIR_NAME%
		DEL %ROBOCOPY_LOG%
	)
	EXIT /B
	
:IS_EXIST_MP4
	FOR /R %1 %%i IN (*.mp4 *.webm *.m4a *.mp3) DO EXIT /B 0
	EXIT /B 1
	
:UPDATE_VIDEO_DB
	REM SET PATH=%PATH%;%USERPROFILE%\PycharmProjects\mp3tag-tool
	SET TARGET_DIR=%1
	IF NOT EXIST %TARGET_DIR% EXIT /B 1
	WHERE update_video_db.py
	IF %ERRORLEVEL% EQU 1 EXIT /B 1
	update_video_db.py %TARGET_DIR%
	EXIT /B

